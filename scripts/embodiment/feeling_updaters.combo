#
# Feeling updaters
#
# @author Zhenhua Cai czhedu@gmail.com
# @date   2011-02-21
#
# Note: Some tricks about combo scripts
#     1. There cannot be any whitespaces between operator and (
#     2. There is no '-' operator, it is the default type of an unrecognized string.
#        So for -x you must write *(-1 x). 
#        But if you want you could define a procedure neg(1) := *(-1 #1) under
#        scripts/embodiment/stdlib.combo
#     3. The best doc about Combo is doc/comboreduct/ComboReduct.pdf in the OpenCog project
#     4. You should avoid infinitely great number in Combo
#     5. Refer to the function 'builtin_str_to_vertex' in file 'opencog/comboreduct/combo/vertex.h', 
#         you will get the lexicon of the built-in combo operators
#

# How emotions emerge from the system?
#
# Step 1. Define a small set of proto-dimentions in terms of basic demands and modulators. 
# Step 2. Different emotions are then indentified with corresponding regions in the space spanned by these dimentions
#
# Step 1 in detail:
#
# We choose five dimentions, all the four modulators plus pleasure, listed below:
#
# 1. Activation. 
#    It controls the agent's readiness for action, the higher the activation, the pressing it has become to react 
#    to the situation at hand, and faster decisions are sought. 
#    A higher activation would lead to few details and less schematic depth. 
#
# 2. Resolution. 
#    It affects perception. 
#    A low resolution level tends to miss differences, then the agent would get a better overview
#
# 3. SecuringThreshold. 
#    The frequency of the securing behavior is inversily determined by SecuringThreshold.
#    The value of securing threshold is proportional to the strength of the current motive, 
#    i.e. in the face of urgency, there will be less orientation. 
#    It also depends on the uncertainty in the current context.
#    An undetermined environment requires more orientation, i.e. lower securing threshold. 
#
# 4. SelectionThreshold.
#    It is a bias added to the strength of the currently selected motive (Demand Goal). 
#    A higher selection threshold leads to "stubbornness", makes it harder to switch motives (Demand Goals), 
#    then oscillations can be avoided.
#    While a lower one results in opportunism/flexibility, or even motive fluttering. 
#
# 5. Pleasure. 
#    For the moment, it is simply measured by how well the previous and current demand goals are satisfied. 
#
# Step 2 in detail:
#
# Different emotions with their corresponding occupation in the space spanned by the dimentions above.
#       
#   Emotion       ||    Activation    Resolution    SecuringThreshold    SelectionThreshold    Pleasure
#  ======================================================================================================
#   fear          ||        H             L                L                                       L 
#  ------------------------------------------------------------------------------------------------------ 
#   pride         ||                      L                H                      H                H 
#  ------------------------------------------------------------------------------------------------------ 
#   love          ||                      EL               EH                     EH               EH 
#  ------------------------------------------------------------------------------------------------------ 
#   hate          ||        EH            EL               EH                                      EL
#  ------------------------------------------------------------------------------------------------------ 
#   anger         ||        H             L                H                      L                L
#  ------------------------------------------------------------------------------------------------------ 
#   gratitude     ||                                                              H                H
#  ------------------------------------------------------------------------------------------------------ 
#   happiness     ||                                       H                      H                H
#  ------------------------------------------------------------------------------------------------------ 
#   excitement    ||       H              L                                       L                EH/EL  
#
#   ( H = high, L = low, M = medium, E = extremely )
#

# Thresholds for fuzzy high, low, medium etc.
modulator_high_threshold(0) := 0.7
modulator_extremely_high_threshold(0) := 0.85
modulator_low_threshold(0) := 0.3
modulator_extremely_low_threshold(0) := 0.15
modulator_medium_threshold(0) := 0.5

# Used by 'modulator_to_feeling_dimension' below
modulator_extremely_low_indicator(0) := -2
modulator_low_indicator(0) := -1
modulator_medium_indicator(0) := 0
modulator_high_indicator(0) := 1
modulator_extremely_high_indicator(0) := 2
modulator_undefined_indicator(0) := 3

# Get pleasure based on previously/ currently selected demand goal
get_pleasure(0) := +( *(0.35 get_current_demand_goal_truth_value)
                      *(0.65 get_previous_demand_goal_truth_value)
                    )

# Map a modulator to corresponding feeling dimension
# #1 the modulator value, 
# #2 equals to -2 means extremely low, -1 low, 0 medium, 1 high, 2 extremely high, others not defined (would return 0) 
modulator_to_feeling_dimension(2) := 
     contin_if( equal_to(#2 modulator_extremely_low_indicator) 
                fuzzy_low(#1 modulator_extremely_low_threshold 100)
                contin_if( equal_to(#2 modulator_low_indicator)
                           fuzzy_low(#1 modulator_low_threshold 100)
                           contin_if( equal_to(#2 modulator_medium_indicator)
                                      fuzzy_equal(#1 modulator_medium_threshold 100)
                                      contin_if( equal_to(#2 modulator_high_indicator)
                                                 fuzzy_high(#1 modulator_high_threshold 100)
                                                 contin_if( equal_to(#2 modulator_extremely_high_indicator)
                                                            fuzzy_high(#1 modulator_extremely_high_threshold 100)
                                                            0
                                                          )
                                               )
                                        
                                    )
                            
                         )
                
              )

# Calculate the feeling based on modulator indicators (low, high, medium etc.)
# #1 indicator for Activation
# #2 indicator for Resolution
# #3 indicator for SecuringThreshold
# #4 indicator for SelectionThreshold
# #5 indicator for Pleasure
# #6 the number of modulators used during calculation 
feeling_calculator(6) :=
    /( +( modulator_to_feeling_dimension( get_activation_modulator #1 )
          +( modulator_to_feeling_dimension( get_resolution_modulator #2 )
             +( modulator_to_feeling_dimension( get_securing_threshold_modulator #3 )
                +( modulator_to_feeling_dimension( get_selection_threshold_modulator #4 )
                   modulator_to_feeling_dimension( get_pleasure #5 )
                 )
              )
           )
        )
       #6
     )                     

# Feeling updaters    
fearFeelingUpdater(0) := feeling_calculator( modulator_high_indicator
                                             modulator_low_indicator
                                             modulator_low_indicator
                                             modulator_undefined_indicator
                                             modulator_low_indicator
                                             4
                                           )

prideFeelingUpdater(0) := feeling_calculator( modulator_undefined_indicator
                                              modulator_low_indicator
                                              modulator_high_indicator
                                              modulator_high_indicator
                                              modulator_high_indicator
                                              4
                                            )

loveFeelingUpdater(0) := feeling_calculator( modulator_undefined_indicator
                                             modulator_extremely_low_indicator
                                             modulator_extremely_high_indicator
                                             modulator_extremely_high_indicator
                                             modulator_extremely_high_indicator
                                             4
                                           )

hateFeelingUpdater(0) := feeling_calculator( modulator_extremely_high_indicator
                                             modulator_extremely_low_indicator
                                             modulator_extremely_high_indicator
                                             modulator_undefined_indicator
                                             modulator_extremely_low_indicator
                                             4
                                           )

angerFeelingUpdater(0) := feeling_calculator( modulator_high_indicator
                                              modulator_low_indicator
                                              modulator_high_indicator
                                              modulator_low_indicator
                                              modulator_low_indicator
                                              5
                                            )

gratitudeFeelingUpdater(0) := feeling_calculator( modulator_undefined_indicator
                                                  modulator_undefined_indicator
                                                  modulator_undefined_indicator
                                                  modulator_high_indicator
                                                  modulator_high_indicator
                                                  2
                                                )

happinessFeelingUpdater(0) := feeling_calculator( modulator_undefined_indicator
                                                  modulator_undefined_indicator
                                                  modulator_high_indicator
                                                  modulator_high_indicator
                                                  modulator_high_indicator
                                                  3
                                                )

excitementFeelingUpdater(0) := max( feeling_calculator( modulator_high_indicator
                                                        modulator_low_indicator
                                                        modulator_undefined_indicator
                                                        modulator_low_indicator
                                                        modulator_extremely_high_indicator
                                                        4
                                                      )

                                    feeling_calculator( modulator_high_indicator
                                                        modulator_low_indicator
                                                        modulator_undefined_indicator
                                                        modulator_low_indicator
                                                        modulator_extremely_low_indicator
                                                        4
                                                      )
                                  )

